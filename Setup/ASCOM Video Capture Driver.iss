; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "ASCOM (DirectShow) Video Capture Driver"
#define MyAppVersion "1.0"
#define MyAppPublisher "ASCOM Initiative"
#define MyAppURL "http://www.hristopavlov.net/ASCOM"
#define TheAppId="{09A80BA2-DEA5-4EA9-9732-27C81C9899DE}"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{#TheAppId}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=setup
SetupIconFile=D:\Work\ASCOM\ASCOM.DirectShow.Video\ASCOM.DirectShow.Video\Setup\ASCOM.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Dirs]
Name: "{app}\bin"

[Files]
Source: "D:\Work\ASCOM\ASCOM.DirectShow.Video\ASCOM.DirectShow.Video\Setup\bin\ASCOM.DirectShow.Video.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "D:\Work\ASCOM\ASCOM.DirectShow.Video\ASCOM.DirectShow.Video\Setup\bin\ASCOM.Provisional.Video.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
Source: "D:\Work\ASCOM\ASCOM.DirectShow.Video\ASCOM.DirectShow.Video\Setup\bin\DirectShowLib-2005.dll"; DestDir: "{app}\bin"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Run]
Filename:"{reg:HKLM\SOFTWARE\Microsoft\.NETFramework,InstallRoot}\v4.0.30319\RegAsm.exe"; Parameters: /codebase ASCOM.DirectShow.Video.dll;WorkingDir: {app}\bin; StatusMsg: "Registering Video Driver ..."; Flags: runhidden

[UninstallRun]
Filename:"{reg:HKLM\SOFTWARE\Microsoft\.NETFramework,InstallRoot}\v4.0.30319\RegAsm.exe"; Parameters: /unregister ASCOM.DirectShow.Video.dll;WorkingDir: {app}\bin; StatusMsg: "Unregistering Video Driver ..."; Flags: runhidden

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, release, serviceCount: cardinal;
    check45, success: boolean;
begin
    // .NET 4.5 installs as update to .NET 4.0 Full
    if version = 'v4.5' then begin
        version := 'v4\Full';
        check45 := true;
    end else
        check45 := false;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0/4.5 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 uses additional value Release
    if check45 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= 378389);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;


function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4\Client', 0) then begin
        MsgBox('ASCOM Video (Provisional) SDK requires Microsoft .NET Framework 4.0 Client Profile.'#13#13
            'Please use Windows Update to install this version,'#13
            'and then re-run the ASCOM Video (Provisional) SDK setup program.', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;


procedure CurStepChanged(CurStep: TSetupStep);
    begin
        case CurStep of
        ssDone:
            // Update the Registry with the Uninstall icon from the unistaller.
            //
            RegWriteStringValue(HKEY_LOCAL_MACHINE,
            'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{#TheAppId}_is1',
            'DisplayIcon',
            ExpandConstant('{uninstallexe}'));
    end;
end;
